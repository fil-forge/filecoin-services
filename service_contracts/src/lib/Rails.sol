// SPDX-License-Identifier: Apache-2.0 OR MIT
pragma solidity ^0.8.20;

import {Errors} from "../Errors.sol";
import {FilecoinPayV1} from "@fws-payments/FilecoinPayV1.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {
    CurrencyAdded,
    CurrencyEnabledSet,
    CurrencyRegistry,
    CurrencyRegistryStorage,
    Currency,
    MAX_CURRENCY_DECIMALS,
    MIN_CURRENCY_DECIMALS,
    DATA_SET_INFO_ROOT_SLOT
} from "./CurrencyRegistry.sol";

interface IDefaultCurrency {
    function usdfcTokenAddress() external view returns (address);
}
import {
    CDN_LOCKUP_PERIOD,
    DATASET_FEE_PER_EPOCH,
    DATASET_FEE_PER_MONTH,
    DEFAULT_CACHE_MISS_LOCKUP_AMOUNT,
    DEFAULT_CDN_LOCKUP_AMOUNT,
    DEFAULT_LOCKUP_PERIOD,
    EPOCHS_PER_MONTH,
    LIFECYCLE_RESERVE_TARGET,
    REPLENISH_THRESHOLD,
    SERVICE_COMMISSION_BPS,
    calculateStorageRateAtPrice
} from "./PriceListUSDFC.sol";
import {DataSetPricing} from "./DataSetPricing.sol";
import {SignatureVerificationLib} from "./SignatureVerificationLib.sol";
import {FilecoinWarmStorageService} from "../FilecoinWarmStorageService.sol";
import {IPDPVerifier} from "@pdp/interfaces/IPDPVerifier.sol";

event CDNPaymentRailsToppedUp(
    uint256 indexed dataSetId,
    uint256 cdnAmountAdded,
    uint256 totalCdnLockup,
    uint256 cacheMissAmountAdded,
    uint256 totalCacheMissLockup
);

event CDNServiceTerminated(
    address indexed caller, uint256 indexed dataSetId, uint256 cacheMissRailId, uint256 cdnRailId
);

event DataSetAbandoned(uint256 indexed dataSetId, uint256 pdpRailId, uint256 cacheMissRailId, uint256 cdnRailId);

event RailRateUpdated(uint256 indexed dataSetId, uint256 railId, uint256 newRate);

library Rails {
    /// @notice Validates the payer is set up to add pieces, not just create the dataset.
    /// @dev    The lifecycle reserve is consumed at creation; the per-dataset fee headroom is only
    ///         consumed once pieces are added. Empty datasets leave it as approved headroom.
    ///         Required up front intentionally: creation assumes pieces will follow.
    /// @param payments The FilecoinPayV1 contract instance
    /// @param usdfcTokenAddress The USDFC token used for deposits and operator approvals
    /// @param payer The address of the payer
    /// @param includeCDN Whether to include fixed CDN/cache-miss lockups in the requirement checks
    /// @param scale Divisor from the 18-decimal price list to the token's units
    function validatePayerOperatorApprovalAndFunds(
        FilecoinPayV1 payments,
        IERC20 usdfcTokenAddress,
        address payer,
        bool includeCDN,
        uint256 scale
    ) internal view {
        // Required capacity: lifecycle reserve plus per-dataset fee lockup at the default period.
        // Multiply-first preserves the exact monthly value for cleaner error messages; slightly
        // more conservative than the actual rail lockup (truncated per-epoch), within 0.0001%
        // and always in the user's favor.
        uint256 requiredLockup =
            (DATASET_FEE_PER_MONTH * DEFAULT_LOCKUP_PERIOD) / EPOCHS_PER_MONTH + LIFECYCLE_RESERVE_TARGET;

        // If CDN is enabled, include the fixed cache-miss and CDN lockup amounts
        if (includeCDN) {
            requiredLockup += DEFAULT_CACHE_MISS_LOCKUP_AMOUNT + DEFAULT_CDN_LOCKUP_AMOUNT;
        }
        // Every term is a whole number of 10**12 units, so dividing the sum is exact
        requiredLockup /= scale;
        uint256 datasetFeePerEpoch = DATASET_FEE_PER_EPOCH / scale;

        // Check that payer has sufficient available funds
        (,, uint256 availableFunds,) = payments.getAccountInfoIfSettled(usdfcTokenAddress, payer);
        require(availableFunds >= requiredLockup, Errors.InsufficientLockupFunds(payer, requiredLockup, availableFunds));

        // Check operator approval settings
        (
            bool isApproved,
            uint256 rateAllowance,
            uint256 lockupAllowance,
            uint256 rateUsage,
            uint256 lockupUsage,
            uint256 maxLockupPeriod
        ) = payments.operatorApprovals(usdfcTokenAddress, payer, address(this));

        // Verify operator is approved
        require(isApproved, Errors.OperatorNotApproved(payer, address(this)));

        // Rate-allowance headroom for the per-dataset fee rate: the floor of any non-empty
        // dataset's rate (size-proportional component sits on top). Empty datasets never consume
        // it; required up front for the dataset to be eligible to receive pieces.
        require(
            rateAllowance >= rateUsage + datasetFeePerEpoch,
            Errors.InsufficientRateAllowance(payer, address(this), rateAllowance, rateUsage, datasetFeePerEpoch)
        );

        // Verify lockup allowance is sufficient
        require(
            lockupAllowance >= lockupUsage + requiredLockup,
            Errors.InsufficientLockupAllowance(payer, address(this), lockupAllowance, lockupUsage, requiredLockup)
        );

        // Verify max lockup period is sufficient
        require(
            maxLockupPeriod >= DEFAULT_LOCKUP_PERIOD,
            Errors.InsufficientMaxLockupPeriod(payer, address(this), maxLockupPeriod, DEFAULT_LOCKUP_PERIOD)
        );
    }

    function createRails(
        FilecoinPayV1 payments,
        uint256 dataSetId,
        IERC20 usdfcTokenAddress,
        address payer,
        address payee,
        address filBeamBeneficiaryAddress
    ) public returns (uint256 pdpRailId, uint256 cacheMissRailId, uint256 cdnRailId) {
        bool hasCDN = filBeamBeneficiaryAddress != address(0);
        uint256 scale = currencyScale(dataSetId);
        // Validate payer has sufficient funds and operator approvals to cover the required lockup
        // If CDN is enabled, validation must account for the additional fixed lockup amounts
        validatePayerOperatorApprovalAndFunds(payments, usdfcTokenAddress, payer, hasCDN, scale);

        pdpRailId = payments.createRail(
            usdfcTokenAddress, // token address
            payer, // from (payer)
            payee, // payee address from registry
            address(this), // this contract acts as the validator
            SERVICE_COMMISSION_BPS, // commission rate based on CDN usage
            address(this)
        );

        // Set lockup period and seed the lifecycle reserve
        payments.modifyRailLockup(pdpRailId, DEFAULT_LOCKUP_PERIOD, LIFECYCLE_RESERVE_TARGET / scale);

        cacheMissRailId = 0;
        cdnRailId = 0;

        if (hasCDN) {
            cacheMissRailId = payments.createRail(
                usdfcTokenAddress, // token address
                payer, // from (payer)
                payee, // payee address from registry
                address(0), // no validator
                0, // no service commission
                address(this) // controller
            );
            uint256 cacheMissLockup = DEFAULT_CACHE_MISS_LOCKUP_AMOUNT / scale;
            payments.modifyRailLockup(cacheMissRailId, CDN_LOCKUP_PERIOD, cacheMissLockup);

            cdnRailId = payments.createRail(
                usdfcTokenAddress, // token address
                payer, // from (payer)
                filBeamBeneficiaryAddress, // to FilBeam beneficiary
                address(0), // no validator
                0, // no service commission
                address(this) // controller
            );
            uint256 cdnLockup = DEFAULT_CDN_LOCKUP_AMOUNT / scale;
            payments.modifyRailLockup(cdnRailId, CDN_LOCKUP_PERIOD, cdnLockup);

            emit CDNPaymentRailsToppedUp(dataSetId, cdnLockup, cdnLockup, cacheMissLockup, cacheMissLockup);
        }
    }

    function terminateCDNRails(FilecoinPayV1 payments, uint256 dataSetId, uint256 cacheMissRailId, uint256 cdnRailId)
        public
    {
        try payments.terminateRail(cacheMissRailId) {} catch {}
        try payments.terminateRail(cdnRailId) {} catch {}
        emit CDNServiceTerminated(msg.sender, dataSetId, cacheMissRailId, cdnRailId);
    }

    /// @notice Tears down all rails for an abandoned data set.
    /// @dev SP forfeits pending op-fees; lifecycle reserve returns to the payer.
    ///      For well-funded payers the rail is terminated with no lockup period, releasing the
    ///      reserve immediately.
    ///      For underfunded payers the PDP rail remains for DEFAULT_LOCKUP_PERIOD; the payer's
    ///      streaming lockup tail isn't released until that period elapses, though every proven epoch is paid
    ///      out first.
    ///      CDN rails are best-effort, may have been terminated externally.
    function abandonRails(
        FilecoinPayV1 payments,
        mapping(uint256 dataSetId => uint256 activationEpoch) storage provingActivationEpoch,
        uint256 dataSetId,
        uint256 pdpRailId,
        uint256 cacheMissRailId,
        uint256 cdnRailId
    ) public {
        payments.settleRail(pdpRailId, block.number);

        // Try to zero the lockup period before termination.
        // For underfunded payers the period change will fail.
        bool reserveRemaining = false;
        try payments.modifyRailLockup(pdpRailId, 0, 0) {}
        catch {
            reserveRemaining = true;
        }

        if (cdnRailId != 0) {
            _teardownCDNRail(payments, cacheMissRailId);
            _teardownCDNRail(payments, cdnRailId);
            emit CDNServiceTerminated(msg.sender, dataSetId, cacheMissRailId, cdnRailId);
        }

        // clearing this allows settling remaining epochs with zero payment
        delete provingActivationEpoch[dataSetId];

        payments.terminateRail(pdpRailId);
        if (reserveRemaining) {
            // release the fixed reserve
            payments.modifyRailLockup(pdpRailId, DEFAULT_LOCKUP_PERIOD, 0);
        }
        payments.settleRail(pdpRailId, block.number);
        emit DataSetAbandoned(dataSetId, pdpRailId, cacheMissRailId, cdnRailId);
    }

    /// @notice Tears down one CDN rail.
    /// @dev Each step may revert if the rail was independently terminated or finalised by the
    ///      payer or FilBeam controller (CDN rails have no validator). Best-effort so abandonment
    ///      completes regardless.
    function _teardownCDNRail(FilecoinPayV1 payments, uint256 railId) internal {
        try payments.modifyRailLockup(railId, 0, 0) {} catch {}
        try payments.terminateRail(railId) {} catch {}
        try payments.settleRail(railId, block.number) {} catch {}
    }

    function topUpCDNRails(
        FilecoinPayV1 payments,
        uint256 dataSetId,
        uint256 cacheMissRailId,
        uint256 cdnRailId,
        uint256 cacheMissAmountToAdd,
        uint256 cdnAmountToAdd
    ) public {
        // Both rails must be active for any top-up operation
        FilecoinPayV1.RailView memory cdnRail = payments.getRail(cdnRailId);
        FilecoinPayV1.RailView memory cacheMissRail = payments.getRail(cacheMissRailId);

        require(cdnRail.endEpoch == 0, Errors.CDNPaymentAlreadyTerminated(dataSetId));
        require(cacheMissRail.endEpoch == 0, Errors.CacheMissPaymentAlreadyTerminated(dataSetId));

        // Require at least one amount to be non-zero
        if (cdnAmountToAdd == 0 && cacheMissAmountToAdd == 0) {
            revert Errors.InvalidTopUpAmount(dataSetId);
        }

        // Calculate total lockup amounts
        uint256 totalCdnLockup = cdnRail.lockupFixed + cdnAmountToAdd;
        uint256 totalCacheMissLockup = cacheMissRail.lockupFixed + cacheMissAmountToAdd;

        // Only modify rails if amounts are being added
        payments.modifyRailLockup(cdnRailId, CDN_LOCKUP_PERIOD, totalCdnLockup);
        payments.modifyRailLockup(cacheMissRailId, CDN_LOCKUP_PERIOD, totalCacheMissLockup);
        emit CDNPaymentRailsToppedUp(
            dataSetId, cdnAmountToAdd, totalCdnLockup, cacheMissAmountToAdd, totalCacheMissLockup
        );
    }

    function settleCDNRails(
        FilecoinPayV1 payments,
        uint256 cdnRailId,
        uint256 cacheMissRailId,
        uint256 cdnAmount,
        uint256 cacheMissAmount
    ) public {
        if (cdnAmount > 0) {
            payments.modifyRailPayment(cdnRailId, 0, cdnAmount);
        }

        if (cacheMissAmount > 0) {
            payments.modifyRailPayment(cacheMissRailId, 0, cacheMissAmount);
        }
    }

    // Replenishes the rail's fixed lockup when the reserve would drop below REPLENISH_THRESHOLD
    // after paying pending. Returns the new lockupFixed value (mirrors lifecycleReserveBalance).
    // Skipped for terminated rails (pdpEndEpoch != 0): modifyRailLockup forbids increases there.
    function replenishReserveIfNeeded(
        FilecoinPayV1 payments,
        uint256 pdpRailId,
        uint256 pdpEndEpoch,
        uint96 reserveBalance,
        uint96 pending,
        uint256 scale
    ) internal returns (uint96) {
        if (pdpEndEpoch == 0 && reserveBalance < pending + uint96(REPLENISH_THRESHOLD / scale)) {
            uint96 newLockup = uint96(LIFECYCLE_RESERVE_TARGET / scale) + pending;
            payments.modifyRailLockup(pdpRailId, DEFAULT_LOCKUP_PERIOD, newLockup);
            return newLockup;
        }
        return reserveBalance;
    }

    function updateStorageRates(
        FilecoinPayV1 payments,
        uint256 dataSetId,
        uint256 pdpRailId,
        uint256 leafCount,
        uint96 pending,
        uint96 reserveBalance,
        uint256 pdpEndEpoch,
        bool immediateTermination
    ) public returns (uint96 newReserveBalance) {
        uint256 scale = currencyScale(dataSetId);
        uint256 newStorageRatePerEpoch =
            calculateStorageRateAtPrice(leafCount, DataSetPricing.effectiveStoragePrice(dataSetId, scale), scale);
        if (immediateTermination) {
            // No try/catch: immediateTermination implies the payer consented and is solvent.
            payments.modifyRailLockup(pdpRailId, 0, pending);
            newReserveBalance = 0;
        } else {
            uint96 replenished =
                replenishReserveIfNeeded(payments, pdpRailId, pdpEndEpoch, reserveBalance, pending, scale);
            if (replenished < pending) {
                pending = replenished;
            }
            newReserveBalance = replenished - pending;
        }
        payments.modifyRailPayment(pdpRailId, newStorageRatePerEpoch, pending);
        emit RailRateUpdated(dataSetId, pdpRailId, newStorageRatePerEpoch);
    }

    /// @notice Divisor from the 18-decimal price list to a data set's token units.
    /// @dev Reads the decimal shift in `DataSetInfo.currency` (struct slot 10, bits 200-207) from FWSS storage;
    ///      Rails functions run by DELEGATECALL in FWSS's context. Zero (every pre-#618 data set) is 1.
    function currencyScale(uint256 dataSetId) internal view returns (uint256 scale) {
        bytes32 baseSlot = DATA_SET_INFO_ROOT_SLOT;
        assembly ("memory-safe") {
            mstore(0, dataSetId)
            mstore(0x20, baseSlot)
            let shift := and(shr(200, sload(add(keccak256(0, 0x40), 10))), 0xff) // currency code bits 8-15
            scale := exp(10, shift)
        }
    }

    // ---------------------------------------------------------------------
    // Currency whitelist administration (#618). FWSS forwards its owner-only
    // `setCurrency(address,bool)` call here unchanged.

    /// @notice Whitelists `token` under the next id, or enables/disables an existing entry.
    /// @dev The default token (id 0) cannot be added. Decimals must be between 6 and 18.
    function setCurrency(address token, bool enabled) public {
        // OwnableUpgradeable's ERC-7201 slot, read directly as upstream's FWSSOwnable does
        address owner;
        assembly ("memory-safe") {
            owner := sload(0x9016d09d72d40fdae2fd8ceac6b6234c7706214fd39c1cd1e609a0528c199300)
        }
        require(msg.sender == owner, OwnableUpgradeable.OwnableUnauthorizedAccount(msg.sender));
        CurrencyRegistryStorage storage $ = CurrencyRegistry.layout();
        uint256 currencyId = $.ids[token];
        if (currencyId == 0) {
            require(token != IDefaultCurrency(address(this)).usdfcTokenAddress(), Errors.CurrencyAlreadyAdded(token));
            uint8 decimals = IERC20Metadata(token).decimals();
            require(
                decimals >= MIN_CURRENCY_DECIMALS && decimals <= MAX_CURRENCY_DECIMALS,
                Errors.InvalidCurrencyDecimals(token, decimals)
            );
            currencyId = $.count + 1;
            require(currencyId <= type(uint8).max, Errors.TooManyCurrencies());
            $.count = currencyId;
            $.ids[token] = currencyId;
            $.currencies[currencyId] = Currency({token: token, decimals: decimals, enabled: enabled});
            emit CurrencyAdded(uint8(currencyId), token, decimals);
        } else {
            $.currencies[currencyId].enabled = enabled;
        }
        emit CurrencyEnabledSet(uint8(currencyId), token, enabled);
    }

    /// @notice Changes a data set's storage price by mutual consent and re-prices its rail.
    /// @dev Body of FilecoinWarmStorageService.updateStoragePrice, kept here so the FWSS core carries only a
    ///      forwarding stub. Runs by DELEGATECALL in the proxy: msg.sender is the original caller, `info` is
    ///      the proxy's DataSetInfo, and immutables are read back through the proxy's public getters.
    function updateStoragePrice(
        FilecoinWarmStorageService.DataSetInfo storage info,
        uint256 dataSetId,
        uint256 storagePricePerTibPerMonth,
        uint256 nonce,
        bytes calldata signature,
        address authorizer,
        bytes32 domainSeparator
    ) public {
        uint256 pdpRailId = info.pdpRailId;
        if (pdpRailId == 0) revert Errors.InvalidDataSetId(dataSetId);
        if (info.pdpEndEpoch != 0) revert Errors.DataSetPaymentAlreadyTerminated(dataSetId);
        address serviceProvider = info.serviceProvider;
        if (msg.sender != serviceProvider) {
            revert Errors.CallerNotServiceProvider(dataSetId, serviceProvider, msg.sender);
        }

        FilecoinWarmStorageService self = FilecoinWarmStorageService(address(this));
        SignatureVerificationLib.verifyUpdateStoragePriceAuthorization(
            info.payer,
            dataSetId,
            authorizer,
            nonce,
            storagePricePerTibPerMonth,
            signature,
            domainSeparator,
            self.sessionKeyRegistry()
        );
        DataSetPricing.updatePrice(dataSetId, storagePricePerTibPerMonth, nonce, currencyScale(dataSetId));

        // Re-price now so an idle data set picks up the new price; applies from the next epoch.
        info.lifecycleReserveBalance = updateStorageRates(
            FilecoinPayV1(self.paymentsContractAddress()),
            dataSetId,
            pdpRailId,
            IPDPVerifier(self.pdpVerifierAddress()).getDataSetLeafCount(dataSetId),
            info.pendingOneTimePayments,
            info.lifecycleReserveBalance,
            0,
            false
        );
        info.pendingOneTimePayments = 0;
    }
}
